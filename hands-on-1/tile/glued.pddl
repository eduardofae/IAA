(define (domain glued-sliding-tile)

  (:requirements :strips :negative-preconditions)

  (:predicates
   (IS-TILE ?x)
   (IS-ROW ?x)
   (IS-COLUMN ?x)
   (NEXT-ROW ?r1 ?r2)
   (NEXT-COLUMN ?c1 ?c2)
   (tile-at ?t ?r ?c)
   (is-blank ?r ?c)
   (GLUED ?x)
   )

  (:action move-tile-down
    :parameters (?tile ?old-row ?new-row ?col)
    :precondition (and (IS-TILE ?tile)
                       (IS-ROW ?old-row)
                       (IS-ROW ?new-row)
                       (IS-COLUMN ?col)
                       (NEXT-ROW ?old-row ?new-row)
                       (tile-at ?tile ?old-row ?col)
                       (is-blank ?new-row ?col)
                       (not (GLUED ?tile)))
    :effect (and (not (tile-at ?tile ?old-row ?col))
                 (not (is-blank ?new-row ?col))
                 (tile-at ?tile ?new-row ?col)
                 (is-blank ?old-row ?col)))

  (:action move-tile-up
    :parameters (?tile ?old-row ?new-row ?col)
    :precondition (and (IS-TILE ?tile)
                       (IS-ROW ?old-row)
                       (IS-ROW ?new-row)
                       (IS-COLUMN ?col)
                       (NEXT-ROW ?new-row ?old-row)
                       (tile-at ?tile ?old-row ?col)
                       (is-blank ?new-row ?col)
                       (not (GLUED ?tile)))
    :effect (and (not (tile-at ?tile ?old-row ?col))
                 (not (is-blank ?new-row ?col))
                 (tile-at ?tile ?new-row ?col)
                 (is-blank ?old-row ?col)))

  (:action move-tile-right
    :parameters (?tile ?row ?old-col ?new-col)
    :precondition (and (IS-TILE ?tile)
                       (IS-ROW ?row)
                       (IS-COLUMN ?old-col)
                       (IS-COLUMN ?new-col)
                       (NEXT-COLUMN ?old-col ?new-col)
                       (tile-at ?tile ?row ?old-col)
                       (is-blank ?row ?new-col)
                       (not (GLUED ?tile)))
    :effect (and (not (tile-at ?tile ?row ?old-col))
                 (not (is-blank ?row ?new-col))
                 (tile-at ?tile ?row ?new-col)
                 (is-blank ?row ?old-col)))

  (:action move-tile-left
    :parameters (?tile ?row ?old-col ?new-col)
    :precondition (and (IS-TILE ?tile)
                       (IS-ROW ?row)
                       (IS-COLUMN ?old-col)
                       (IS-COLUMN ?new-col)
                       (NEXT-COLUMN ?new-col ?old-col)
                       (tile-at ?tile ?row ?old-col)
                       (is-blank ?row ?new-col)
                       (not (GLUED ?tile)))
    :effect (and (not (tile-at ?tile ?row ?old-col))
                 (not (is-blank ?row ?new-col))
                 (tile-at ?tile ?row ?new-col)
                 (is-blank ?row ?old-col)))

  )
